plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'org.ajoberstar.grgit'
}

// Used to get commit of current HEAD and store it in
import org.ajoberstar.grgit.Grgit
Grgit grgit = Grgit.open(dir: '../')

android {
    buildToolsVersion "29.0.2"
    compileSdkVersion 29

	lintOptions {
		lintConfig file("lint.xml")
	}

    defaultConfig {
        defaultPublishConfig 'release'
        publishNonDefault true

        applicationId "com.launchkey.android.authenticator.demo"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }

    flavorDimensions "languageApp"
    productFlavors {
        javaApp {
            dimension "languageApp"
            applicationIdSuffix ".javaApp"
        }
        kotlinApp {
            dimension "languageApp"
            applicationIdSuffix ".kotlinApp"
        }
    }
    buildTypes {
        String[] releaseProguardFiles = [getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro']
        release {
            minifyEnabled true
            proguardFiles releaseProguardFiles
            resValue("string", "demo_commit_hash", grgit.head().id)
        }
        debug {
            minifyEnabled true
            proguardFiles releaseProguardFiles + 'debug-proguard-rules.pro'
            resValue("string", "demo_commit_hash", grgit.head().id)
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support
    // TODO: Update to androidX for the Auth SDK Sample App
    def supportLibsVersion = "28.0.0"
    implementation "com.android.support:support-v4:$supportLibsVersion"
    implementation "com.android.support:appcompat-v7:$supportLibsVersion"
    implementation "com.android.support:design:$supportLibsVersion"

    // Google Play Services
    implementation "com.google.android.gms:play-services-base:16.1.0"
    implementation "com.google.android.gms:play-services-location:16.0.0"
    implementation "com.google.android.gms:play-services-wearable:16.0.1"

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.7"
    implementation "com.google.firebase:firebase-messaging:17.3.4"

    // Open Street Maps updated to latest version for API 29
    implementation 'org.osmdroid:osmdroid-android:6.1.2@aar'

    // Auth SDK
    implementation files('libs/lk-auth-sdk/lk-auth-sdk-4.9.0.aar')
}

// Applies the lk-proguard-rules and adds dependencies for the Auth SDK
apply from: 'libs/lk-auth-sdk/LaunchKeyPlugin.gradle'

apply plugin: 'com.google.gms.google-services'